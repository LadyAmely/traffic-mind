plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.springdoc.openapi-gradle-plugin" version "1.9.0"
    id 'checkstyle'
}

group = 'org.shop'
version = '0.0.1-SNAPSHOT'
description = 'traffic'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

checkstyle {
    toolVersion = "10.12.4"
    configFile = file("checkstyle.xml")
    ignoreFailures = false
    showViolations = true
    configProperties = [
            'checkstyle.suppresssion.file':file("checkstyle-suprressions.xml").absolutePath]
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    testImplementation 'com.tngtech.archunit:archunit-junit5:1.4.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform{
        if(project.hasProperty('skipIntegrationTests')){
            excludeTags 'integration'
        }
    }
}

tasks.register('style'){
    group = 'verification'
    dependsOn("checkstyleMain", "checkstyleTest")
}

tasks.withType(Checkstyle).configureEach {
    reports{
        html.required = false
        xml.required = true
    }
}